# Copyright 2021 Associated Universities, Inc. Washington DC, USA.
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH "@PACKAGE_cmakeModulesDir@")

set(enable_serial @HPG_ENABLE_SERIAL@)
set(enable_openmp @HPG_ENABLE_OPENMP@)
set(enable_cuda @HPG_ENABLE_CUDA@)

unset(extra_args)
if(${CMAKE_FIND_PACKAGE_NAME}_FIND_QUIETLY)
  list(APPEND extra_args QUIET)
endif()
if(${CMAKE_FIND_PACKAGE_NAME}_FIND_REQUIRED)
  list(APPEND extra_args REQUIRED)
endif()

set(KOKKOS_COMPONENTS kokkos kokkoscore)
if(enable_serial)
  list(APPEND ENABLED_DEVICES SERIAL)
endif()
if(enable_openmp)
  list(APPEND ENABLED_DEVICES OPENMP)
endif()
if(enable_cuda)
  list(APPEND ENABLED_DEVICES CUDA)
  list(APPEND KOKKOS_COMPONENTS CUDA)
endif()
find_package(Kokkos
  COMPONENTS ${KOKKOS_COMPONENTS}
  ${extra_args})
if(Kokkos_FOUND)
  kokkos_check(DEVICES ${ENABLED_DEVICES})
endif()

if(enable_serial OR enable_openmp)
  set(FFTW_COMPONENTS)
  if(enable_serial)
    list(APPEND FFTW_COMPONENTS Double)
  endif()
  if(enable_openmp)
    list(APPEND FFTW_COMPONENTS DoubleOpenMP)
  endif()
  find_package(FFTW COMPONENTS ${FFTW_COMPONENTS} ${extra_args})
endif()

find_dependency(Threads)

include("${CMAKE_CURRENT_LIST_DIR}/hpgTargets.cmake")
set(hpg_FOUND True)
check_required_components(hpg)
if(hpg_FOUND)
  message(STATUS "Found HPG: ${CMAKE_CURRENT_LIST_DIR}/hpgConfig.cmake")
endif()
